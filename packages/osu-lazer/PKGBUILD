# Maintainer: Tuhana GAYRETLÄ° <hi at tuhana dot me>
# shellcheck disable=SC2034,SC2148,SC2164,SC2154

pkgname=osu-lazer
pkgver=2022.1031.0
pkgrel=1
pkgdesc='An open source, free-to-win rhythm game. Rhythm is just a click away!'
arch=(x86_64)
url='https://osu.ppy.sh'
license=(MIT)
depends=(libgl)
options=(!strip)
source=(
  "https://github.com/ppy/osu/releases/download/$pkgver/osu.AppImage"
  'https://github.com/ppy/osu/raw/master/assets/lazer-nuget.png'
  'https://github.com/ppy/osu/raw/master/LICENCE'
  'sh.ppy.osu.lazer'
  'sh.ppy.osu.lazer.desktop'
  'sh.ppy.osu.lazer.xml'
)
noextract=('osu.AppImage')
sha256sums=(
  '5e0ada5379c4de4b2a9f9c351f2ddd9783b064f13e4c0cbe4a30eea92ab26080'
  '4adebcd0ad5e32f2e770c580fc8e6c7b4b4b4304bf15180b88bfb4db3f1b64f7'
  '3c3c252e45e3506fda9c44628ee8ec4ba06cf582c69fe1ef2d1ee3ef93bafa47'
  '32a83fbd6a91ac734972440a690d8b398140b3b00404022a172040ff5efc6d6f'
  '142e38d1ae1e215cee0a78ca781cfcafad37f788537cf23958d18a445f19a898'
  'e6ec9d911b12511681431fa86322c3904b0ab0faf4c5afe6671fa0b3beb5813c'
)

prepare() {
  cd "$srcdir"

  chmod +x osu.AppImage
  ./osu.AppImage --appimage-extract >/dev/null
}

package() {
  cd "$srcdir"

  # Launch script, desktop entry and MIME type
  install -Dm755 -t "$pkgdir/usr/bin" sh.ppy.osu.lazer
  install -Dm644 -t "$pkgdir/usr/share/applications" sh.ppy.osu.lazer.desktop
  install -Dm644 -t "$pkgdir/usr/share/mime/packages" sh.ppy.osu.lazer.xml

  # Icons and license
  install -Dm644 squashfs-root/osu!.png "$pkgdir/usr/share/icons/hicolor/1024x1024/apps/osu!lazer.png"
  install -Dm644 lazer-nuget.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/osu!lazer.png"
  install -Dm644 -t "$pkgdir/usr/share/licenses" LICENCE

  # Game files
  install -d "$pkgdir/opt/osu!lazer"
  cp -r squashfs-root/usr/bin/. "$pkgdir/opt/osu!lazer"
}
